plugins {
    id("java-library")
    id("org.jetbrains.kotlin.jvm") version "1.4.31"
    id("com.github.rahulsom.waena.published")
    id("io.miret.etienne.sass") version "1.1.1"
}

apply plugin: 'groovy'
apply from: "$rootDir/gradle/jacoco.gradle"

description = "Asciidoctor Extension to generate Event Sourcing Diagrams like those on https://rahulsom.github.io/grooves"

repositories {
    google()
}

dependencies {
    implementation 'com.github.rahulsom:svg-builder:0.2.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.31"

    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'org.asciidoctor:asciidoctorj:1.5.4.1'
    implementation "org.slf4j:slf4j-api:1.7.30"

    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-core:3.0.0'
    implementation 'com.sun.xml.bind:jaxb-impl:3.0.0'
    implementation 'javax.activation:activation:1.1.1'

    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.4'
}

compileGroovy.dependsOn compileKotlin
compileGroovy.classpath += files(compileKotlin.destinationDir)
classes.dependsOn compileGroovy

compileSass {
    sourceDir = project.file ("${projectDir}/src/main/resources")
    outputDir = project.file ("${buildDir}/generated/css")
}

sourceSets {
    main {
        resources.srcDirs "${buildDir}/generated/css"
    }
}

tasks.getByName("processResources").dependsOn("compileSass")