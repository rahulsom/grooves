buildscript {
    repositories {
        gradlePluginPortal()
        google()
    }
    dependencies {
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:7.2.1'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.71'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
    }
}

apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "org.jetbrains.dokka"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'groovy'
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"

description = "Asciidoctor Extension to generate Event Sourcing Diagrams like those on https://rahulsom.github.io/grooves"

repositories {
    google()
}

dependencies {
    compile 'com.github.rahulsom:svg-builder:0.2.3'
    compile('com.github.sommeri:less4j:1.17.2') {
        exclude group: 'com.google.protobuf'
    }
    compile 'org.codehaus.groovy:groovy-all:2.4.+'
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.71'

    compile 'commons-beanutils:commons-beanutils:1.9.3'
    compile 'org.asciidoctor:asciidoctorj:1.5.4.1'
    compile "org.slf4j:slf4j-api:$slf4jVersion"

    compile 'javax.xml.bind:jaxb-api:2.2.11'
    compile 'com.sun.xml.bind:jaxb-core:2.2.11'
    compile 'com.sun.xml.bind:jaxb-impl:2.2.11'
    compile 'javax.activation:activation:1.1.1'

    testCompile 'org.spockframework:spock-core:1.2-groovy-2.4'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

compileGroovy.dependsOn compileKotlin
compileGroovy.classpath += files(compileKotlin.destinationDir)
classes.dependsOn compileGroovy