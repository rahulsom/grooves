plugins {
    id("java-library")
    id("groovy")
    id("org.jetbrains.kotlin.jvm")
    id("org.jlleitschuh.gradle.ktlint")
    id("com.github.rahulsom.waena.published")
    id("io.miret.etienne.sass") version "1.1.2"
}

apply from: "$rootDir/gradle/jacoco.gradle"

description = "Asciidoctor Extension to generate Event Sourcing Diagrams like those on https://rahulsom.github.io/grooves"

repositories {
    google()
}

dependencies {
    implementation 'com.github.rahulsom:svg-builder:0.3.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'org.asciidoctor:asciidoctorj:2.5.1'
    implementation "org.slf4j:slf4j-api:1.7.31"

    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-core:3.0.1'
    implementation 'com.sun.xml.bind:jaxb-impl:3.0.1'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.4'

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

compileGroovy.dependsOn compileKotlin
compileGroovy.classpath += files(compileKotlin.destinationDir)
classes.dependsOn compileGroovy

compileSass {
    sourceDir = project.file ("${projectDir}/src/main/resources")
    outputDir = project.file ("${buildDir}/generated/css")
}

sourceSets {
    main {
        resources.srcDirs "${buildDir}/generated/css"
    }
}

tasks.getByName("processResources").dependsOn("compileSass")