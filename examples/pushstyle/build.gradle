buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.2" }
    }
    dependencies {
        classpath "com.h2database:h2:latest.release"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    id "org.jlleitschuh.gradle.ktlint" version "3.0.0"
    id "nu.studer.jooq" version "2.0.9"
    id "org.flywaydb.flyway" version "5.1.4"
}

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.2" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:latest.release"
    compile "org.jetbrains.kotlin:kotlin-reflect:latest.release"
    compile 'org.codehaus.groovy:groovy-all:2.4.+'

    compile 'com.google.inject:guice:latest.release'
    compile 'com.google.guava:guava:23.4-jre'

    compile "com.h2database:h2:latest.release"
    compile 'org.jooq:jooq'

    compile project(':grooves-api')

    jooqRuntime "com.h2database:h2:latest.release"

    runtime 'ch.qos.logback:logback-classic:1.+'

    testCompile 'junit:junit:4.12'
    testCompile 'org.awaitility:awaitility:3.0.0'
}

sonarqube {
    properties {
        property 'sonar.moduleKey', 'com.github.rahulsom:grooves:examples-pushstyle'
        property 'sonar.exclusions', '**/generated-src/**'
    }
}

jooq {
    version = '3.10.1'
    edition = 'OSS'
    main(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = "jdbc:h2:file:${buildDir}/schema"
            user = 'sa'
            password = ''
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.h2.H2Database'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'grooves.example.pushstyle'
            }
        }
    }
}

flyway {
    url = "jdbc:h2:file:${buildDir}/schema"
    user = 'sa'
    password = ''
    schemas = ['public']
}

generateMainJooqSchemaSource.dependsOn 'flywayMigrate'
compileKotlin.dependsOn 'generateMainJooqSchemaSource'

checkstyleMain.source = "src/main/java"
