buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.h2database:h2:1.4.197'
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'nu.studer:gradle-jooq-plugin:2.0.9'
        classpath 'gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:5.2.1'

        classpath 'javax.xml.bind:jaxb-api:2.2.11'
        classpath 'com.sun.xml.bind:jaxb-core:2.2.11'
        classpath 'com.sun.xml.bind:jaxb-impl:2.2.11'
        classpath 'javax.activation:activation:1.1.1'
    }
}

apply plugin: 'nu.studer.jooq'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'org.flywaydb.flyway'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile 'org.codehaus.groovy:groovy-all:2.4.15'

    compile 'com.google.inject:guice:4.2.1'
    compile 'com.google.guava:guava:27.0-jre'

    compile 'com.h2database:h2:1.4.197'
    compile 'org.jooq:jooq'

    compile 'javax.xml.bind:jaxb-api:2.2.11'
    compile 'com.sun.xml.bind:jaxb-core:2.2.11'
    compile 'com.sun.xml.bind:jaxb-impl:2.2.11'
    compile 'javax.activation:activation:1.1.1'
    compileOnly 'javax.annotation:javax.annotation-api:1.2'

    compile project(':grooves-api')

    compile 'io.reactivex.rxjava2:rxjava:2.2.3'
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    jooqRuntime 'com.h2database:h2:1.4.197'
    jooqRuntime 'javax.xml.bind:jaxb-api:2.2.11'
    jooqRuntime 'com.sun.xml.bind:jaxb-core:2.2.11'
    jooqRuntime 'com.sun.xml.bind:jaxb-impl:2.2.11'
    jooqRuntime 'javax.activation:activation:1.1.1'

    runtime 'ch.qos.logback:logback-classic:1.2.3'

    testCompile 'junit:junit:4.+'
    testCompile 'org.awaitility:awaitility:3.1.2'
}

jooq {
    version = '3.10.1'
    edition = 'OSS'
    main(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = "jdbc:h2:file:${buildDir}/schema"
            user = 'sa'
            password = ''
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.h2.H2Database'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'grooves.example.pushstyle'
            }
        }
    }
}

flyway {
    url = "jdbc:h2:file:${buildDir}/schema"
    user = 'sa'
    password = ''
    schemas = ['public']
}

generateMainJooqSchemaSource.dependsOn 'flywayMigrate'
compileKotlin.dependsOn 'generateMainJooqSchemaSource'

tasks.findByName('checkstyleMain')?.source = "src/main/java"
