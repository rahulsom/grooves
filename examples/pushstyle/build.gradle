buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.2" }
    }
    dependencies {
        classpath "com.h2database:h2:latest.release"
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:latest.release"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:latest.release"
        classpath "nu.studer:gradle-jooq-plugin:2.0.9"
        classpath "gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:latest.release"

        classpath 'javax.xml.bind:jaxb-api:2.2.11'
        classpath 'com.sun.xml.bind:jaxb-core:2.2.11'
        classpath 'com.sun.xml.bind:jaxb-impl:2.2.11'
        classpath 'javax.activation:activation:1.1.1'

    }
}

apply plugin: "nu.studer.jooq"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "org.flywaydb.flyway"

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.2" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:latest.release"
    compile "org.jetbrains.kotlin:kotlin-reflect:latest.release"
    compile 'org.codehaus.groovy:groovy-all:latest.release'

    compile 'com.google.inject:guice:latest.release'
    compile 'com.google.guava:guava:latest.release'

    compile "com.h2database:h2:latest.release"
    compile 'org.jooq:jooq'

    compile 'javax.xml.bind:jaxb-api:2.2.11'
    compile 'com.sun.xml.bind:jaxb-core:2.2.11'
    compile 'com.sun.xml.bind:jaxb-impl:2.2.11'
    compile 'javax.activation:activation:1.1.1'

    compile project(':grooves-api')

    jooqRuntime "com.h2database:h2:latest.release"
    jooqRuntime 'javax.xml.bind:jaxb-api:2.2.11'
    jooqRuntime 'com.sun.xml.bind:jaxb-core:2.2.11'
    jooqRuntime 'com.sun.xml.bind:jaxb-impl:2.2.11'
    jooqRuntime 'javax.activation:activation:1.1.1'

    runtime 'ch.qos.logback:logback-classic:latest.release'

    testCompile 'junit:junit:4.+'
    testCompile 'org.awaitility:awaitility:latest.release'
}

jooq {
    version = '3.10.1'
    edition = 'OSS'
    main(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = "jdbc:h2:file:${buildDir}/schema"
            user = 'sa'
            password = ''
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.h2.H2Database'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'grooves.example.pushstyle'
            }
        }
    }
}

flyway {
    url = "jdbc:h2:file:${buildDir}/schema"
    user = 'sa'
    password = ''
    schemas = ['public']
}

generateMainJooqSchemaSource.dependsOn 'flywayMigrate'
compileKotlin.dependsOn 'generateMainJooqSchemaSource'

checkstyleMain.source = "src/main/java"
