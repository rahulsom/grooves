buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.h2database:h2:1.4.197'
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.2.1'
        classpath 'nu.studer:gradle-jooq-plugin:2.0.9'
        classpath 'gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:5.2.1'

        classpath 'javax.xml.bind:jaxb-api:2.2.11'
        classpath 'com.sun.xml.bind:jaxb-core:2.2.11'
        classpath 'com.sun.xml.bind:jaxb-impl:2.2.11'
        classpath 'javax.activation:activation:1.1.1'
    }
}

plugins {
    id("org.jetbrains.kotlin.jvm")
}

apply plugin: 'nu.studer.jooq'
apply plugin: 'org.flywaydb.flyway'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.31"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.31"
    implementation 'org.codehaus.groovy:groovy-all:2.4.15'

    implementation 'com.google.inject:guice:4.2.1'
    implementation 'com.google.guava:guava:27.0-jre'

    implementation 'com.h2database:h2:1.4.197'
    implementation 'org.jooq:jooq'

    implementation 'javax.xml.bind:jaxb-api:2.2.11'
    implementation 'com.sun.xml.bind:jaxb-core:2.2.11'
    implementation 'com.sun.xml.bind:jaxb-impl:2.2.11'
    implementation 'javax.activation:activation:1.1.1'
    compileOnly 'javax.annotation:javax.annotation-api:1.2'

    implementation project(':grooves-api')

    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
    implementation "org.slf4j:slf4j-api:1.7.30"

    jooqRuntime 'com.h2database:h2:1.4.197'
    jooqRuntime 'javax.xml.bind:jaxb-api:2.2.11'
    jooqRuntime 'com.sun.xml.bind:jaxb-core:2.2.11'
    jooqRuntime 'com.sun.xml.bind:jaxb-impl:2.2.11'
    jooqRuntime 'javax.activation:activation:1.1.1'

    runtime 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'junit:junit:4.+'
    testImplementation 'org.awaitility:awaitility:3.1.2'
}

jooq {
    version = '3.10.1'
    edition = 'OSS'
    main(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = "jdbc:h2:file:${buildDir}/schema"
            user = 'sa'
            password = ''
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.h2.H2Database'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'grooves.example.pushstyle'
            }
        }
    }
}

flyway {
    url = "jdbc:h2:file:${buildDir}/schema"
    user = 'sa'
    password = ''
    schemas = ['public']
}

generateMainJooqSchemaSource.dependsOn 'flywayMigrate'
compileKotlin.dependsOn 'generateMainJooqSchemaSource'

tasks.findByName('checkstyleMain')?.source = "src/main/java"
