buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.bmuschko:gradle-cargo-plugin:2.2.3"
    }
}

apply plugin: "com.bmuschko.cargo"
apply plugin: 'war'
apply plugin: 'groovy'

version = '0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    providedCompile 'javax:javaee-api:7.0'

    compileOnly 'org.projectlombok:lombok:1.18.6'
    compileOnly "org.jetbrains:annotations:16.0.3"

    compile project(':grooves-java')
    compile 'org.apache.commons:commons-lang3:3.8.1'
    compile 'io.reactivex:rxjava-reactive-streams:1.2.1'
    compile 'io.reactivex:rxjava:1.3.8'

    cargo 'org.codehaus.cargo:cargo-core-uberjar:1.6.11'
    cargo 'org.codehaus.cargo:cargo-ant:1.6.11'

    compile project(':grooves-example-test')
}

cargo {
    containerId = 'wildfly10x'
    deployable {
        context = 'grooves-jee'
    }
    local {
        installer {
            installUrl = 'http://download.jboss.org/wildfly/10.1.0.Final/wildfly-10.1.0.Final.zip'
            downloadDir = file("${gradle.gradleUserHomeDir}/cargo")
            extractDir = file("$buildDir/extract")
            outputFile = file('build/output.log')
        }
    }
}

cargoRunLocal.dependsOn 'war'
cargoStartLocal.dependsOn 'war'

int statusOfUrl(String url) {
    int status = -1
    try {
        content = url.toURL().openConnection().with { conn ->
            readTimeout = 10000
            status = responseCode
        }
    } catch (Exception ignore) {
        // ignore
    }
    status
}

cargoStartLocal.doLast {
    int attempt = 0
    while (attempt < 20) {
        println "Waiting (${++attempt})..."
        if (statusOfUrl('http://localhost:8080/grooves-jee/patient') == 200) {
            break
        }
        sleep 5000
    }
}

test.dependsOn 'cargoStartLocal'
test.finalizedBy 'cargoStopLocal'
