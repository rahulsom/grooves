buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.2.3'
    }
}

apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'com.bmuschko.cargo'

version = '0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    providedCompile 'javax:javaee-api:7.0'
    compile project(':grooves-api')
    compile 'org.apache.commons:commons-lang3:3.5'

    def cargoVersion = '1.6.3'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
            "org.codehaus.cargo:cargo-ant:$cargoVersion"

    testCompile project(':grooves-example-test')
}

cargo {
    containerId = 'wildfly10x'
    deployable {
        context = 'grooves-jee'
    }
    local {
        installer {
            installUrl = 'http://download.jboss.org/wildfly/10.1.0.Final/wildfly-10.1.0.Final.zip'
            downloadDir = file("$buildDir/download")
            extractDir = file("$buildDir/extract")
            outputFile = file('build/output.log')
        }
    }
}

cargoRunLocal.dependsOn 'war'
cargoStartLocal.dependsOn 'war'

int statusOfUrl(String url) {
    int status = -1
    try {
        content = url.toURL().openConnection().with { conn ->
            readTimeout = 10000
            status = responseCode
        }
    } catch (Exception ignore) {
        // ignore
    }
    status
}

cargoStartLocal.doLast {
    int attempt = 0
    while (attempt < 20) {
        sleep 5000
        println "Waiting (${++attempt})..."
        if (statusOfUrl('http://localhost:8080/grooves-jee/patient') == 200) {
            break
        }
    }
}

test.dependsOn 'cargoStartLocal'
test.finalizedBy 'cargoStopLocal'