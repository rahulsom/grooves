plugins {
    id("org.ajoberstar.git-publish").version("2.1.3")
    id("org.asciidoctor.jvm.convert").version("3.3.2")
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/release' }
}

configurations {
    asciidoctorExt
}

dependencies {
    asciidoctorExt project(':grooves-diagrams')
    asciidoctorExt 'io.spring.asciidoctor:spring-asciidoctor-extensions-block-switch:0.6.0'
}

project.version = rootProject.version.toString()

asciidoctor {
    sourceDir file('src/docs/asciidoc')
    baseDir file('src/docs/asciidoc')
    sources {
        include 'index.adoc'
    }
    configurations 'asciidoctorExt'

    attributes (
        'sourcedir': rootDir.absolutePath,
        'includedir': file('src/docs/asciidoc'),
        'toc': 'left',
        'icons': 'font',
        'stylesheet': 'grooves.css',
        'source-highlighter': 'rouge' ,
    )

    resources {
        from("src/docs/asciidoc") {
            include("*.css")
        }
    }
}

asciidoctor.dependsOn ':grooves-diagrams:jar'

gitPublish {
    repoUri.set "https://github.com/rahulsom/grooves.git"
    branch.set 'gh-pages'
    contents {
        from(file("$buildDir/asciidoc/html5")) {
            into "manual/${version}"
        }
        if (System.getenv('BRANCH_NAME') == 'master') {
            from(file("$buildDir/asciidoc/html5")) {
                into "manual/current"
            }
            from(rootProject.file('grooves-site')) {
                into '.'
            }
        }
    }
    preserve {
        include '**'
    }
}

tasks.findByName('gitPublishPush').dependsOn 'asciidoctor'
