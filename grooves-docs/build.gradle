buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.ajoberstar:gradle-git-publish:2.1.3"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.6.1"
    }
}

apply plugin: "org.ajoberstar.git-publish"
apply plugin: "org.asciidoctor.convert"

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    asciidoctor project(':grooves-diagrams')
    asciidoctor 'com.github.spring-io:spring-asciidoctor-extensions:05bca0c665'
}

asciidoctor {
    sources {
        include 'index.adoc'
    }
    attributes 'sourcedir': rootDir.absolutePath,
            'version': project.version.toString(),
            'toc': 'left',
            'icons': 'font',
            'stylesheet': 'grooves.css'
}

asciidoctor.dependsOn ':grooves-diagrams:jar'
def env = System.getenv()

gitPublish {
    repoUri = 'https://github.com/rahulsom/grooves.git'
    branch = 'gh-pages'
    contents {
        from(file("$buildDir/asciidoc/html5")) {
            into "manual/${version}"
        }
        if (env['BRANCH_NAME'] == 'master') {
            from(file("$buildDir/asciidoc/html5")) {
                into "manual/current"
            }
            from(rootProject.file('grooves-site')) {
                into '.'
            }
        }
    }
    preserve {
        include '**'
    }
}

tasks.findByName('gitPublishPush').dependsOn 'asciidoctor'
