buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id("com.github.rahulsom.waena.root").version("0.4.0")
    id("org.jetbrains.kotlin.jvm").version("1.5.31").apply(false)
    id("org.jetbrains.kotlin.plugin.allopen").version("1.5.31").apply(false)
    id("org.jlleitschuh.gradle.ktlint").version("11.0.0").apply(false)
    id("org.sonarqube").version("3.1.1")
    id("me.champeau.buildscan-recipes").version("0.2.3")
}

allprojects {
    group = 'com.github.rahulsom'
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }
    apply plugin: 'checkstyle'

    checkstyle {
        configFile = rootProject.file('gradle/checkstyle/checkstyle.xml')
        toolVersion = "7.6.1"
        showViolations = false
        maxWarnings = 0
        maxErrors = 0
    }

    def theProject = project
    def projectPath = theProject.name
    while (theProject.parent != rootProject) {
        projectPath = "${theProject.parent.name}-${projectPath}"
        theProject = theProject.parent
    }

    sonarqube {
        properties {
            property 'sonar.moduleKey', "com.github.rahulsom:grooves:${projectPath}"
        }
    }
}

contacts {
    validateEmails = true
    'rahulsom@noreply.github.com' {
        moniker("Rahul Somasunderam")
        roles("owner")
        github("https://github.com/rahulsom")
    }
}

tasks.getByName('release').dependsOn 'grooves-docs:gitPublishPush'

buildScanRecipes {
    recipes 'git-commit', 'git-status', 'travis-ci', 'gc-stats'
}
