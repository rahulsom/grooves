buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "me.champeau.gradle:buildscan-recipes-plugin:0.2.3"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.1.1"
        classpath "com.netflix.nebula:nebula-release-plugin:15.3.1"
    }
}

apply plugin: "me.champeau.buildscan-recipes"
apply plugin: "io.codearte.nexus-staging"
apply plugin: "org.sonarqube"
apply plugin: nebula.plugin.publishing.maven.MavenNebulaPublishPlugin
apply plugin: "com.dorongold.task-tree"

allprojects {
    group = 'com.github.rahulsom'
    apply plugin: 'nebula.release'
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }
    apply plugin: 'checkstyle'

    checkstyle {
        configFile = rootProject.file('gradle/checkstyle/checkstyle.xml')
        toolVersion = "7.6.1"
        showViolations = false
        maxWarnings = 0
        maxErrors = 0
    }

    def theProject = project
    def projectPath = theProject.name
    while (theProject.parent != rootProject) {
        projectPath = "${theProject.parent.name}-${projectPath}"
        theProject = theProject.parent
    }

    sonarqube {
        properties {
            property 'sonar.moduleKey', "com.github.rahulsom:grooves:${projectPath}"
        }
    }
}

// tasks.getByName('release').dependsOn 'grooves-docs:gitPublishPush'

buildScanRecipes {
    recipes 'git-commit', 'git-status', 'travis-ci', 'gc-stats'
}
