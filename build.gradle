buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.bmuschko:gradle-cargo-plugin:2.8.0"

        classpath "me.champeau.gradle:buildscan-recipes-plugin:0.2.3"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0"

        classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.4.1'
    }
}

plugins {
    id("com.github.rahulsom.waena.root").version("0.2.1")
    id("org.jetbrains.kotlin.jvm").version("1.4.31").apply(false)
}

apply plugin: "me.champeau.buildscan-recipes"
apply plugin: "org.sonarqube"

allprojects {
    group = 'com.github.rahulsom'
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }
    apply plugin: 'checkstyle'

    checkstyle {
        configFile = rootProject.file('gradle/checkstyle/checkstyle.xml')
        toolVersion = "7.6.1"
        showViolations = false
        maxWarnings = 0
        maxErrors = 0
    }

    def theProject = project
    def projectPath = theProject.name
    while (theProject.parent != rootProject) {
        projectPath = "${theProject.parent.name}-${projectPath}"
        theProject = theProject.parent
    }

    sonarqube {
        properties {
            property 'sonar.moduleKey', "com.github.rahulsom:grooves:${projectPath}"
        }
    }
}

// tasks.getByName('release').dependsOn 'grooves-docs:gitPublishPush'

buildScanRecipes {
    recipes 'git-commit', 'git-status', 'travis-ci', 'gc-stats'
}
