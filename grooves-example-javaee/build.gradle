plugins {
    id("com.bmuschko.cargo").version("2.8.0")
    id("war")
    id("groovy")
}

version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    providedCompile 'javax:javaee-api:8.0.1'

    compileOnly 'org.projectlombok:lombok:1.18.6'
    compileOnly "org.jetbrains:annotations:16.0.3"

    implementation project(':grooves-java')
    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'io.reactivex:rxjava-reactive-streams:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation project(':grooves-example-test')

    cargo 'org.codehaus.cargo:cargo-core-uberjar:1.7.10'
    cargo 'org.codehaus.cargo:cargo-ant:1.10.1'

}

cargo {
    containerId = 'wildfly10x'
    deployable {
        context = 'grooves-jee'
    }
    local {
        installer {
            installUrl = 'https://download.jboss.org/wildfly/10.1.0.Final/wildfly-10.1.0.Final.zip'
            downloadDir = file("${gradle.gradleUserHomeDir}/cargo")
            extractDir = file("$buildDir/extract")
            outputFile = file('build/output.log')
        }
    }
}

cargoRunLocal.dependsOn 'war'
cargoStartLocal.dependsOn 'war'

int statusOfUrl(String url) {
    int status = -1
    try {
        content = url.toURL().openConnection().with { conn ->
            readTimeout = 10000
            status = responseCode
        }
    } catch (Exception ignore) {
        // ignore
    }
    status
}

cargoStartLocal.doLast {
    int attempt = 0
    while (attempt < 20) {
        println "Waiting (${++attempt})..."
        if (statusOfUrl('http://localhost:8080/grooves-jee/patient') == 200) {
            break
        }
        sleep 5000
    }
}

test.dependsOn 'cargoStartLocal'
test.finalizedBy 'cargoStopLocal'
